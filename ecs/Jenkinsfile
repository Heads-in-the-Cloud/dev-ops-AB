#!groovy
pipeline {
    agent any

    parameters {
        string(
            name: 'ProjectId',
            defaultValue: 'AB-utopia',
            description: 'Identifier applied to all names'
        )
        choice(
            name: 'Env',
            choices: ['Dev', 'Staging', 'Prod'],
            description: 'Deployment environment'
        )
        choice(
            name: 'Operation',
            choices: ['Up', 'Down'],
            description: 'Bring up or take down ECS cluster'
        )
    }

   environment {
        COMMIT_HASH = sh(
            returnStdout: true,
            script: "git rev-parse --short=8 HEAD"
        ).trim()
        S3_BUCKET = params.ProjectId.toLowerCase()
        REGION = sh(
            script: 'aws configure get region',
            returnStdout: true
        ).trim()
        AWS_ACCOUNT_ID = sh(
            script: '''
                aws sts get-caller-identity \
                    --query "Account" \
                    --output text
            ''',
            returnStdout: true
        ).trim()

   }

    stages {
        stage('Configure Environment Inputs') {
            steps {
                script {
                    sh "aws s3 cp s3://$S3_BUCKET/env:/${params.Env}/output.txt ."
                    def tf_output = readProperties file: 'output.txt'
                    env.DOMAIN = tf_output.domain
                    env.VPC_ID = tf_output.vpc_id
                    env.DB_URL = "mysql://${tf_output.db_url}/utopia"
                    env.ALB_ID = tf_output.alb_id

                    def ecr_uri = "${AWS_ACCOUNT_ID}.dkr.ecr.${REGION}.amazonaws.com"
                    echo ecr_uri
                    def image_prefix = "$ecr_uri/${params.ProjectId.toLowerCase()}"
                    env.REVERSE_PROXY_IMAGE = "$image_prefix-reverse-proxy:latest"
                    env.USERS_IMAGE    = "$image_prefix-users-microservice:latest"
                    env.BOOKINGS_IMAGE = "$image_prefix-bookings-microservice:latest"
                    env.FLIGHTS_IMAGE  = "$image_prefix-flights-microservice:latest"

                    withCredentials([
                        string(
                            credentialsId: "${params.Env.toLowerCase()}/${params.ProjectId}/default",
                            variable: 'SECRETS'
                        )
                    ]) {
                        def aws_secrets = readJSON text: SECRETS
                        env.DB_USERNAME = aws_secrets.db_username
                        env.DB_PASSWORD = aws_secrets.db_password
                        env.JWT_SECRET  = aws_secrets.jwt_secret
                    }
                }
            }
        }

        stage('Deploy ECS') {
            when {
                expression {
                    params.Operation == "Up"
                }
            }
            steps {
                dir("ecs") {
                    sh "docker context use ${params.ProjectId}-ecs"
                    sh "docker compose up"
                    sh "docker context default"
                }
            }
        }

        stage('Destroy ECS') {
            when {
                expression {
                    params.Operation == "Down"
                }
            }
            steps {
                dir("ecs") {
                    sh "docker context use ${params.ProjectId}-ecs"
                    sh "docker compose down"
                    sh "docker context default"
                }
            }
        }
    }
}
