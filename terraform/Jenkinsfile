#!groovy
pipeline {
    agent any

    parameters {
        string(
            name: 'ProjectId',
            defaultValue: 'AB-utopia',
            description: 'Identifier applied to all names'
        )
        choice(
            name: 'Env',
            choices: ['Dev', 'Staging', 'Prod'],
            description: 'Deployment environment'
        )
        booleanParam(
            name: 'PlanOnly',
            defaultValue: true,
            description: 'Determines whether or not to run the final stage'
        )
        choice(
            name: 'Operation',
            choices: ['Apply', 'Destroy'],
            description: 'Base infrastructure operation'
        )
    }

   environment {
        COMMIT_HASH = sh(
            script: "git rev-parse --short=8 HEAD",
            returnStdout: true
        ).trim()
        S3_BUCKET = params.ProjectId.toLowerCase()
        REGION = sh(
            script:'aws configure get region',
            returnStdout: true
        ).trim()
    }

    stages {
        stage('Plan') {
            steps {
                dir("terraform") {
                    sh """cat > terraform.tf << EOF
terraform {
  backend "s3" {
    bucket  = "$S3_BUCKET"
    key     = "terraform.tfstate"
    region  = "$REGION"
    encrypt = true
  }
}
EOF
                    """
                    sh """cat > terraform.tfvars << EOF
region = "$REGION"
project_id = "${params.ProjectId}"
environment = "${params.Env.toLowerCase()}"
EOF
                    """
                    sh 'terraform init -input=false -migrate-state'
                    sh "terraform workspace select ${params.Env} || terraform workspace new ${params.Env}"
                    script {
                        if(params.Operation == "Apply") {
                            sh "terraform plan -input=false -out plan.bin"
                        } else {
                            sh "terraform plan -destroy -input=false -out plan.bin"
                        }
                    }
                    sh "terraform show plan.bin"
                }
            }
        }

        stage('Apply') {
            when {
                expression {
                    !params.PlanOnly && params.Operation == "Apply"
                }
            }

            steps {
                dir("terraform") {
                    sh "terraform apply -input=false plan.bin"
                    sh 'terraform refresh'
                    sh 'terraform output > output.txt'
                    sh "aws s3 cp output.txt s3://$S3_BUCKET/env:/${params.Env}/"
                }
            }
        }

        stage('Destroy') {
            when {
                expression {
                    !params.PlanOnly && params.Operation == "Destroy"
                }
            }

            steps {
                dir("terraform") {
                    sh "terraform apply -input=false plan.bin"
                }
            }
        }
    }
}
